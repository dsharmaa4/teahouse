ext.buildTime = new Date().toInstant()

buildscript {
    ext.springBootVersion = 'latest.release'
    ext.springCloudVersion = 'latest.release'
    ext.springDataVersion = 'latest.release'

    configurations.classpath {
        resolutionStrategy.activateDependencyLocking()
    }

    repositories {
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'org.ajoberstar.grgit:grgit-gradle:latest.release'
        classpath 'com.gorylenko.gradle-git-properties:gradle-git-properties:latest.release'
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
    }
}

task clean

apply plugin: 'org.ajoberstar.grgit'
apply from: "$rootDir/gradle/idea.gradle"

allprojects {
    task cleanAll {
        description = 'Deletes the build directory and the IDE files and directories'
        group = 'build'
        dependsOn clean, cleanIdea, cleanIdeaWorkspace
        doLast {
            delete 'out', '.idea'
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply from: "$rootDir/gradle/dependency-locking.gradle"

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    group = 'org.example.teahouse'
    version = "${grgit.head().abbreviatedId}.${buildTime.toEpochMilli()}"

    repositories {
        mavenCentral()
    }

    dependencies {
        ['annotationProcessor', 'compileOnly', 'implementation', 'runtimeOnly', 'testImplementation'].each { conf ->
            dependencies.add(conf, platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion"))
            dependencies.add(conf, platform("org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"))
            dependencies.add(conf, platform("org.springframework.data:spring-data-bom:$springDataVersion"))
            dependencies.add(conf, platform('com.wavefront:wavefront-spring-boot-bom:latest.release'))
            dependencies.add(conf, platform('org.zalando:logbook-bom:latest.release'))
        }
        testImplementation platform('org.junit:junit-bom:latest.release')

        annotationProcessor 'org.projectlombok:lombok'
        compileOnly 'org.projectlombok:lombok'

        testImplementation 'org.junit.jupiter:junit-jupiter-api'
        testImplementation 'org.junit.jupiter:junit-jupiter-params'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    }

    configurations.all {
        resolutionStrategy {
            dependencySubstitution {
                // com.wavefront:wavefront-sdk-java:2.6.5 is broken if you use Sleuth but Micrometer 1.7.4 depends on 2.6.5
                substitute module('com.wavefront:wavefront-sdk-java') with module('com.wavefront:wavefront-sdk-java:2.6.4')
            }
        }
    }

    jar.manifest.attributes(
        'Name': project.name,
        'Implementation-Build-Date': buildTime,
        'Implementation-Title': project.name,
        'Implementation-Version': project.version,
        'Implementation-Vendor': project.group,
        'Commit-AbbreviatedId': grgit.head().abbreviatedId,
        'Commit-Id': grgit.head().id,
        'Commit-Date': grgit.head().getDate().toInstant(),
        'Source-Path': grgit.remote.list().findResult { it.name == 'origin' ? it.url : null },
        'Created-By': "Gradle ${gradle.gradleVersion} with Gradle Ghost (grg)",
        'Built-By': System.properties['user.name'],
        'JDK': org.gradle.internal.jvm.Jvm.current(),
        'OS': org.gradle.internal.os.OperatingSystem.current()
    )

    test {
        useJUnitPlatform()
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}
